
;complex permittivity of brine:
function eps_br, nu, t, s
;+_____________________________________________________________________________
;
; usage:	eps=eps_br(nu, t, s)
;
; purpose:	Calculates complex relative permittivity of brine.
;		From Ulaby et al., Mircrowave Remote Sensing:
;		Active and Passive, Voume III (1986)
;
; input:	nu	Frequence in GHz
;
;		t	Temperature in degrees Celsius
;
;		s	Salinity in psu ~= ppt
;-_____________________________________________________________________________

  nu1=nu*1.e9
  eps0=8.85418782e-12

  ;normality of the solution:
  nb=0.9141*s*(1.707e-2 + s*(1.205e-5 + 4.058e-9*s))
  delta=25.-t

  ;real permittivity of pure water in the large frequency limit:
  eps_inf=4.9

  ;real permittivity of pure water in the stationary limit:
  epsw0=88.045 - t*(0.4147 + t*(6.295e-4 + t*1.075e-5))

  taub02pi=1.1109e-10+T*(-3.824e-12 + T*(6.938e-14 - T*5.096e-16))

  a1=1.0 + nb*(-0.255 + nb*(5.15e-2 - 6.89e-3*nb))
  b1=1.0 + nb*(0.146e-2*t - 4.89e-2 + nb*(-2.97e-2 + 5.64e-3*nb))
  c1=1. + delta*(-1.96e-2 + 8.08e-5*delta) - $
		nb*delta*(3.02e-5 + 3.92e-5*delta + $
		nb*(1.72e-5 - 6.58e-6*delta))


  ;real permittivity of brine in the stationary limit:
  epsb0=epsw0*a1

  tau2pi=taub02pi*b1

  ;conductivity:
  sigma25=nb*(10.39 + nb*(-2.378 + nb*(0.683 + nb*(-0.135 + 1.01e-2*nb))))
  sigma=sigma25*c1

  tmp1=(epsb0 - eps_inf)/(1+(tau2pi*nu1)^2)
  eps_re=eps_inf+tmp1
  eps_im=tau2pi*nu1*tmp1+sigma/2/!pi/nu1/eps0

  ;stop

  return, complex(eps_re, eps_im)

end

;conductivity of saline water:
function sigma_sw, t, s

  ;print, "sigma_sw", t, s

  t2=t*t
  t3=t2*t
  t4=t3*t

  s2=s*s

  ;conductivity of brine from Maetzler 2006:
  sigma35=2.903602+8.607e-2*t+4.738817e-4*t2-2.991e-6*t3+4.3041e-9*t4
  p=s*(37.5109+5.45216*s+0.014409*s2)/(1004.75+182.283*s+s2)

  alpha0=(6.9431+3.2841*s-0.099486*s2)/(84.85+69.024*s+s2)
  alpha1=49.843-0.2276*s+0.00198*s2

  q=1+alpha0*(t-15)/(t+alpha1)

  return, sigma35*p*q

end

;complex permittivity of ocean water as a function
;of temperature and salinity:
function eps_oc, nu, t, s
  ;from C. Maetzler (2006) "Thermal Microwave Radiation: Applications for 
  ;Remote Sensing"

  ;print, "eps_oc", nu, t, s

  nu1=nu*1.e-9
  
  eps_s0=(3708.6-82.168*t)/(421.854+t)
  eps1_0=0.787*eps_s0
  tau1_0=(255.04+0.7246*t)/(49.25+t)/(45+t)
  tau2_0=0.00628
  eps_inf0=4.05+0.0186*t

  a=1-s*(0.03838+0.00218*s)*(79.88+t)/(12.01+s)/(52.53+t)
  b=1-s*(0.03409+0.002817*s)/(7.69+s)-s*t*(0.00246+0.00141*t)/(1880+(-7.57+t)*t)
  c=a
  d=1
  e=1

  eps_s=eps_s0*a
  tau1=tau1_0*b
  eps1=eps1_0*c
  tau2=tau2_0*d
  eps_inf=eps_inf0*e

  sigma=sigma_sw(t, s)

  i=complex(0, 1)
  eps=(eps_s-eps1)/(1-i*nu1*tau1)+(eps1-eps_inf)/(1-i*nu1*tau2)+eps_inf+$
		i*17.9751*sigma/nu1

  ;stop

  return, eps

end

;copied more or less verbatim from Fortran SFT routines:
function eps_oc2, fnu, t, s
      TC=T-.273D3
      EP0=.8774D2-TC*(.40008-TC*(.9398D-3-.141D-5*TC))
      DELAM=FNU*(.111093-TC*(.38236D-2-TC*(.69375D-4-.50963D-6*TC)))
      IF S EQ 0.D0 then GOTO, lab1
      XN=S*(.1707D-1+S*(.12053D-4+.40575D-8*S))
      F=1.D0-XN*(.255059-XN*(.51514D-1-.68889D-2*XN))
      EP0=EP0*F
      F=1.D0-XN*(.48963D-1+XN*(.29667D-1-.56444D-2*XN))
      DELAM=DELAM*(F+.1463D-2*XN*TC)
lab1:     EP1=(EP0-.49D1)/(1.D0+DELAM*DELAM)
      B=DELAM*EP1
      IF S EQ 0.D then GOTO, lab2
      SIG=S*(.1825208-S*(.146192D-2-S*(.209324D-4-.128205D-6*S)))
      TAU=.25D2-TC
      A=.203318D-1+TAU*(.12664D-3+.24637D-5*TAU)-S*(.184911D-4-TAU*(0.25506D-6-.25506D-7*TAU))
      SIGMA=SIG*EXP(-TAU*A)
      B=B+SIGMA/(.55632485D-1*FNU)
lab2:     CKW=complex(.49D1+EP1,B)


  return, ckw

end


;salinity of brine as function of temperature:
function brine_salinity, t
  comp1=t gt -43.2
  comp2=t gt -36.8
  comp3=t gt -22.9
  comp4=t gt -8.2
  comp5=t gt -2.

  sb=fltarr(n_elements(t))-1

  ind=where(comp5, cnt)
  if cnt ne 0 then begin
    s1=0.
    s2=37.6514
    t1=0.
    t2=-2.
    sb[ind]=s1+(t[ind]-t1)/(t2-t1)*(s2-s1)
  endif

  ind=where(comp1 and not comp2, cnt)
  if cnt ne 0 then begin
    t1=t[ind]
    sb[ind]=508.18+t1*(14.535 + t1*0.2018)
  endif

  ind=where(comp2 and not comp3, cnt)
  if cnt ne 0 then begin
    t1=t[ind]
    sb[ind]=242.94 + t1*(1.5299 + 0.0429*t1)
  endif

  ind=where(comp3 and not comp4, cnt)
  if cnt ne 0 then begin
    t1=t[ind]
    sb[ind]=57.041 - t1*(9.929 + t1*(0.16204 + 0.002396*t1))
  endif

  ind=where(comp4 and not comp5, cnt)
  if cnt ne 0 then begin
    t1=t[ind]
    sb[ind]=1.725 - t1*(18.756 + t1*0.3964)
  endif

  return, sb

end

;brine volume as a function of temperature and salinity:
function brine_volume, t, s
  comp1=t lt -0.5
  comp2=t lt -2.06
  comp3=t lt -8.2
  comp4=t ge -22.9

  n=n_elements(t)
  vb=fltarr(n)-1

  ind=where(comp1 and comp2 ne 1, cnt)
  if cnt ne 0 then vb[ind]=s[ind]*(-52.56/t[ind]+2.28)
  ;if cnt ne 0 then vb[ind]=s[ind]*(-52.56/t[ind])
  ind=where(comp2 and comp3 ne 1, cnt)
  if cnt ne 0 then vb[ind]=s[ind]*(-45.917/t[ind]+0.930)
  ind=where(comp3 and comp4, cnt)
  if cnt ne 0 then vb[ind]=s[ind]*(-43.795/t[ind]+1.189)

  return, vb/1000.

end

;complex permittivity of sea ice as a function of
;frequency, temperature and salinity:
function eps_si, nu, t, s

  ;mixture model from Naoki et al. 2008:
  ;brine volume:
  vb=s*(-49.185/t+0.532)/1000.
  ;vb=brine_volume(t, s)
  
  ;sb=s/vb

  sb=brine_salinity(t)

  ;print, "Brine volume:", vb
  ;print, "Salinity of brine:", sb

  ;stop

  ;complex permittivity of brine pockets:
  ;eps_b=eps_br(nu, t, sb)
  ;print, "Complex permittivity brine pockets:", eps_b

  ;real permittivity of pure ice:
  eps_pi=3.1884+9.1e-4*t
  ;print, "Real permittivity pure ice:", eps_pi

  ;simple mixture model:
  ;result=complex(eps_pi/(1-3*vb), 0.5*vb*imaginary(eps_b))
  ;if brine volume is greater than 1/3, then model as pure brine:
  ind=where(vb ge 1./3., cnt)
    ;result=eps_br(nu, t, s)
  result=complex(eps_pi/(1-3*vb), 0.5*vb)

  ;if brine volume is greater than 1/3, then model as pure brine:
  ind=where(vb ge 1./3., cnt)
  if cnt ne 0 then begin
    result[ind]=eps_br(nu, t[ind], s[ind])
    print, "Brine eps:", result[ind]
  endif

  ;result=complex(3.12+vb, 0.5*vb)

  ;stop

  return, result

end


;simplest model, consisting of the basic Fresnel equations:
pro sea_ice_Fresnel, nu, zangle, t, s, rh, rv, eps
  ;calculate complex permittivity of sea ice:
  eps=eps_si(nu, t, s)
  n=sqrt(eps)

  ;calculate transmission angle:
  theta=asin(sin(zangle)/n)

  ;Fresnel equations:
  rh=((cos(zangle)-n*cos(theta))/(cos(zangle)+n*cos(theta)))^2
  rv=((n*cos(zangle)-cos(theta))/(n*cos(zangle)+cos(theta)))^2

  ;stop

end

;fresnel equations:
pro calc_fresnel, zangle, eps, rv, rh
  
  n=sqrt(eps)

  ;calculate transmission angle:
  theta=asin(sin(zangle)/n)

  ;Fresnel equations:
  rh=((cos(zangle)-n*cos(theta))/(cos(zangle)+n*cos(theta)))^2
  rv=((n*cos(zangle)-cos(theta))/(n*cos(zangle)+cos(theta)))^2
end


